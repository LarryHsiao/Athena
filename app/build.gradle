apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

def keystorePropertiesFile = file("signing/variable.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def variablePropertiesFile = file("variable.properties")
def variableProperties = new Properties()
variableProperties.load(new FileInputStream(variablePropertiesFile))

jacoco {
    toolVersion = "0.7.9"
}
final int BUILD_VERSION = 1518456430 + (System.currentTimeMillis()/(1000*60*60))
System.out.println(BUILD_VERSION)
android {
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../publish.json')
        }
    }
    signingConfigs {
        keystore {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file('signing/keystore.jks')
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.silverhetch.athena"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode BUILD_VERSION
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.keystore
        playAccountConfig = playAccountConfigs.defaultAccountConfig

        buildConfigField "String", "AZURE_TRANSLATION_API_KEY", variableProperties['azureTranslateApiKey']
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
    productFlavors {
    }
}

play {
    track = 'beta'
    untrackOld = true // will untrack 'alpha' while upload to 'beta'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.silverhetch:aura:1.0.5@aar'
    implementation 'com.silverhetch:clotho:1.0.2'
    implementation 'com.google.firebase:firebase-core:11.6.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-appindexing:11.6.2'
    implementation 'com.google.firebase:firebase-invites:11.6.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def name = variant.name
        def testTaskName = "test${name.capitalize()}UnitTest"

        tasks.create(name: "${testTaskName}Coverage", type: JacocoReport, dependsOn: "$testTaskName") {
            group = "Reporting"
            description = "Generate Jacoco coverage reports for the ${name.capitalize()} build."

            classDirectories = fileTree(
                    dir: "${project.buildDir}/intermediates/classes/${name}",
                    includes: ['**/com/silverhetch/athena/**'],
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewInjector*.*',
                               '**/*$ViewBinder*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*']
            )

            sourceDirectories = files(['src/main/java'].plus(android.sourceSets[name].java.srcDirs))
            executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

            reports {
                xml.enabled = true
                html.enabled = true
            }
        }
    }
}
apply plugin: 'com.google.gms.google-services'